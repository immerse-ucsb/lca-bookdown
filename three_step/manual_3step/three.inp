TITLE:
Step3 - 3step LSAY
DATA:
FILE = "C:/Users/dnajiarch/Box/lca-bookdown/three_step/manual_3step/
Step3.dat"; 
VARIABLE:
NAMES = ENJOY USEFUL LOGICAL JOB ADULT FEMALE MOTHED MATH_IRT CPROB1 CPROB2 CPROB3
     CPROB4 N; 
 MISSING=.;
 nominal=N;
  usevar = n;
  
  classes = c(4);
  
  usevar = female mothed math_irt;
DEFINE:
center female mothed (grandmean);
ANALYSIS:
estimator = mlr; 
  type = mixture; 
  starts = 0;
MODEL:
%OVERALL%
 
math_irt on female mothed; ! covariate as a related to the distal outcome
C on female (f1-f3);
c on mothed (e1-e3);      ! covariate as predictor of C

   %C#1%
[n#1@3.52]; ! MUST EDIT if you do not have a 4-class model. 
[n#2@-6.756];
[n#3@-0.808];

[math_irt](m1);    ! conditional distal mean 
math_irt;          ! conditional distal variance (freely estimated)

%C#2%
[n#1@7.237];
[n#2@13.687];
[n#3@11.689];

[math_irt](m2);
math_irt;

%C#3%
[n#1@0.991];
[n#2@-0.255];
[n#3@2.051];

[math_irt](m3);
math_irt;

%C#4%
[n#1@-1.516];
[n#2@-5.321];
[n#3@-6.56];

[math_irt](m4);
math_irt; 
MODEL CONSTRAINT:
New (diff12 diff13 diff23 
    diff14 diff24 diff34
    
    d_fem_12 d_fem_13 
    d_fem_23
 
    d_ed_12 d_ed_13 
    d_ed_23 
    );
  
    diff12 = m1-m2;  ! test pairwise distal mean differences
    diff13 = m1-m3;
    diff23 = m2-m3;
    diff14 = m1-m4;
    diff24 = m2-m4;
    diff34 = m3-m4;
 
    d_fem_12 = f1-f2;
    d_fem_13 = f1-f3;
    d_fem_23 = f2-f3; 

    d_ed_12 = e1-e2;
    d_ed_13 = e1-e3;
    d_ed_23 = e2-e3; 

 
MODEL TEST:
     ! omnibus test of distal means 
  !  m1=m2;
  !  m2=m3;
  !  m3=m4;
 
  ! f1=f2;       ! omnibus test of covariate logits (female)  
  !  f1=f3;
 
    e1=e2;       ! omnibus test of covariate logits (mothers ed)  
    e1=e3;
   
