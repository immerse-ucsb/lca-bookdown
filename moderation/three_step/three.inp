TITLE:
LCA Moderation
DATA:
FILE = "C:/Users/dnajiarch/Box/lca-bookdown/moderation/three_step/mod.dat"; 
VARIABLE:
NAMES = ENJOY USEFUL LOGICAL JOB ADULT FEMALE MATH_IRT CPROB1 CPROB2 CPROB3 CPROB4
     N; 
 MISSING=.;
 nominal=N;
  usevar = n;

  classes = c(4);
  
  usevar = female math_irt;
ANALYSIS:
estimator = mlr; 
  type = mixture; 
  starts = 0;
MODEL:
!DISTAL = math_irt, COVARIATE = female, MODERATOR = C
 
%OVERALL%
math_irt on female;
math_irt;

   %C#1%
[n#1@1.516];
[n#2@-3.805];
[n#3@-5.044];

math_irt on female(s1);  ! conditional slope (class 1)
[math_irt](m1);          ! conditional distal mean
math_irt;                ! conditional distal variance (freely estimated)

%C#2%
[n#1@-7.237];
[n#2@6.45];
[n#3@4.451];

math_irt on female(s2);
[math_irt](m2);
math_irt;

%C#3%
[n#1@-0.991];
[n#2@-1.246];
[n#3@1.06];

math_irt on female(s3);
[math_irt](m3);
math_irt;

%C#4%
[n#1@-3.52];
[n#2@-10.276];
[n#3@-4.328];

math_irt on female(s4);
[math_irt](m4);
math_irt; 
MODEL CONSTRAINT:
New (
   diff12 diff13 diff23
   diff14 diff24 diff34
   slope12 slope13 slope23 
    slope14 slope24 slope34);
    
    diff12 = m1-m2;  ! test distal outcome differences
    diff13 = m1-m3;
    diff23 = m2-m3;
    diff14 = m1-m4;
    diff24 = m2-m4;
    diff34 = m3-m4;
  
    slope12 = s1-s2;  ! test pairwise slope differences
    slope13 = s1-s3;
    slope23 = s2-s3;
    slope14 = s1-s4;
    slope24 = s2-s4;
    slope34 = s3-s4;
MODEL TEST:
 ! can run only a single Omnibus test per model 
    s1=s2;
    s2=s3;
    s3=s4;
