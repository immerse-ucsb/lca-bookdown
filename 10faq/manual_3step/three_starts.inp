TITLE:
Step3 - 3step LSAY
DATA:
FILE = "C:/Users/dnajiarch/Box/IES_IMMERSE/bookdown materials/LCA 10 FAQ 
Replication/manual_3step/Step3.dat"; 
VARIABLE:
NAMES = PYDI1AB PYDI2AB PYDI3AB PYDI4AB PYDI5AB PYDI6AB PYDI7AB HISPANIC LIFESATA
     CPROB1 CPROB2 CPROB3 CPROB4 N; 
 MISSING=.;
 nominal=N;
  usevar = n;
  
  classes = c(4);
  
  usevar = hispanic LifeSatA;
DEFINE:
center hispanic (grandmean);
ANALYSIS:
estimator = mlr; 
  type = mixture; 
  starts = 0;
MODEL:
%OVERALL%
 
LifeSatA on hispanic; ! covariate as a predictor of the distal outcome
C on hispanic;        ! covariate as predictor of C

   %C#1%
[n#1@3.547]; ! MUST EDIT if you do not have a 4-class model. 
[n#2@-2.222];
[n#3@-4.641];

[LifeSatA](m1);    ! conditional distal mean 
LifeSatA;          ! conditional distal variance (freely estimated)

%C#2%
[n#1@11.149];
[n#2@13.721];
[n#3@9.942];

[LifeSatA](m2);
LifeSatA;

%C#3%
[n#1@-2.419];
[n#2@0.818];
[n#3@2.321];

[LifeSatA](m3);
LifeSatA;

%C#4%
[n#1@-0.241];
[n#2@-13.232];
[n#3@-5.155];

[LifeSatA](m4);
LifeSatA; 
MODEL CONSTRAINT:
New (diff12 diff13 diff23 
    diff14 diff24 diff34);
  
    diff12 = m1-m2;  ! test pairwise distal mean differences
    diff13 = m1-m3;
    diff23 = m2-m3;
    diff14 = m1-m4;
    diff24 = m2-m4;
    diff34 = m3-m4;
MODEL TEST:
     ! omnibus test of distal means 
    m1=m2;
    m2=m3;
    m3=m4;
