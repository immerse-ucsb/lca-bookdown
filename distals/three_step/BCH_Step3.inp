TITLE:
Step3 - BCH Method
DATA:
FILE = "C:/Users/dnajiarch/Box/lca-bookdown/distals/three_step/
BCH_Step3.dat"; 
VARIABLE:
NAMES = ENJOYM GOODM UNDRSTDM NERVOUSM SCAREDM FEMALE MATHJOB MATHIRT BCHW1 BCHW2
     BCHW3 BCHW4 CPROB1 CPROB2 CPROB3 CPROB4 N; 
 MISSING=.;
 classes = c(4);
  
  missing are all(9999);
  
  usevar = BCHW1-BCHW4 mathjob mathirt female;
  
  training = BCHW1-BCHW4(bch);
ANALYSIS:
estimator = mlr; 
  type = mixture; 
  starts = 500 200;
MODEL:
%OVERALL%

mathirt on female; ! covariate as a predictor of the distal outcome
mathjob on female;
C on female;        ! covariate as predictor of C

%C#1%
   
[mathirt](m1);    ! conditional distal mean 
mathirt;          ! conditional distal variance (freely estimated)
[mathjob](j1);
mathjob;

mathirt on female (rm1);
mathjob on female (rj1);

%C#2%

[mathirt](m2);
mathirt;
[mathjob](j2);
mathjob;

mathirt on female (rm2);
mathjob on female (rj2);

%C#3%

[mathirt](m3);
mathirt;
[mathjob](j3);
mathjob;

mathirt on female (rm3);
mathjob on female (rj3);

%C#4%

[mathirt](m4);
mathirt; 
[mathjob](j4);
mathjob;
 
 mathirt on female (rm4);
 mathjob on female (rj4);
MODEL CONSTRAINT:
New (dm1v2 dm1v3 dm2v3 dm1v4 dm2v4 dm3v4
   dj1v2 dj1v3 dj2v3 dj1v4 dj2v4 dj3v4
    );
  
    dm1v2 = m1-m2;  ! test pairwise distal mean differences of IRT score
    dm1v3 = m1-m3;
    dm2v3 = m2-m3;
    dm1v4 = m1-m4;
    dm2v4 = m2-m4;
    dm3v4 = m3-m4;
    dj1v2 = j1-j2;  ! test pairwise distal mean differences of math job
    dj1v3 = j1-j3;
    dj2v3 = j2-j3;
    dj1v4 = j1-j4;
    dj2v4 = j2-j4;
    dj3v4 = j3-j4;
MODEL TEST:
     ! omnibus test of distal means
    m1=m2;
    m2=m3;
    m3=m4;
    j1=j2;
    j2=j3;
    j3=j4;
 
OUTPUT:
Tech1 svalues sampstat
